%prefix "packcr::parser"

statement <- comment
           / spaces
           / directive_include
           / directive_string
           / rule
           / footer
           / [^\n]* { @errnum += 1; warn "err #{__0.inspect} #{__0sl.linenum}, #{__0sl.charnum}" }

comment <- '#' [^\n]* lf

directive_include <- '%earlysource' opt_spaces_or_comments blocks:code_blocks { blocks.each { |b| @esource  << Packcr::CodeBlock.new(b, Packcr.find_trailing_blanks(b), $0sl.linenum, $0sl.charnum) } }
                   / '%earlycommon' opt_spaces_or_comments blocks:code_blocks { blocks.each { |b| @ecommon  << Packcr::CodeBlock.new(b, Packcr.find_trailing_blanks(b), $0sl.linenum, $0sl.charnum) } }
                   / '%source'      opt_spaces_or_comments blocks:code_blocks { blocks.each { |b| @source   << Packcr::CodeBlock.new(b, Packcr.find_trailing_blanks(b), $0sl.linenum, $0sl.charnum) } }
                   / '%lateheader'  opt_spaces_or_comments blocks:code_blocks { blocks.each { |b| @lheader  << Packcr::CodeBlock.new(b, Packcr.find_trailing_blanks(b), $0sl.linenum, $0sl.charnum) } }
                   / '%latesource'  opt_spaces_or_comments blocks:code_blocks { blocks.each { |b| @lsource  << Packcr::CodeBlock.new(b, Packcr.find_trailing_blanks(b), $0sl.linenum, $0sl.charnum) } }
                   / '%header'      opt_spaces_or_comments blocks:code_blocks { blocks.each { |b| @header   << Packcr::CodeBlock.new(b, Packcr.find_trailing_blanks(b), $0sl.linenum, $0sl.charnum) } }
                   / '%common'      opt_spaces_or_comments blocks:code_blocks { blocks.each { |b| @common   << Packcr::CodeBlock.new(b, Packcr.find_trailing_blanks(b), $0sl.linenum, $0sl.charnum) } }
                   / '%location'    opt_spaces_or_comments blocks:code_blocks { blocks.each { |b| @location << Packcr::CodeBlock.new(b, Packcr.find_trailing_blanks(b), $0sl.linenum, $0sl.charnum) } }
                   / '%initialize'  opt_spaces_or_comments blocks:code_blocks { blocks.each { |b| @init     << Packcr::CodeBlock.new(b, Packcr.find_trailing_blanks(b), $0sl.linenum, $0sl.charnum) } }
code_blocks <- blocks:code_blocks opt_spaces_or_comments block:lang_code_block { blocks.push(block) if block; $$ = blocks }
             / block:lang_code_block { $$ = block ? [block] : [] }

directive_string <- '%value' opt_spaces_or_comments str:quotation_double { @value_type = str }
                  / '%auxil' opt_spaces_or_comments str:quotation_double { @auxil_type = str }
                  / '%prefix' opt_spaces_or_comments str:quotation_double { @prefix = str }

rule <- name:identifier opt_spaces_or_comments '<-' opt_spaces_or_comments expr:expression {
  rule =  Packcr::Node::RuleNode.new(expr, name, $0sl.linenum, $0sl.charnum)
  @rules << rule
}

expression <- expr:expression opt_spaces_or_comments '/' opt_spaces_or_comments seq:sequence { $$ = expr.alt(seq) }
            / seq:sequence { $$ = seq }

sequence <- seq:sequence opt_spaces_or_comments expr:term { $$ = seq.seq(expr) }
          / expr:term { $$ = expr }

term <- expr:pred opt_spaces_or_comments '~' opt_spaces_or_comments code:lang_code_block { $$ = Packcr::Node::ErrorNode.new(expr, Packcr::CodeBlock.new(code, Packcr.find_trailing_blanks(code), $0sl.linenum, $0sl.charnum)) }
      / expr:pred { $$ = expr }

pred <- '&' opt_spaces_or_comments node:quantity { $$ = Packcr::Node::PredicateNode.new(node) }
      / '!' opt_spaces_or_comments node:quantity { $$ = Packcr::Node::PredicateNode.new(node, true) }
      / node:quantity { $$ = node }

quantity <- node:primary opt_spaces_or_comments '*' { $$ = Packcr::Node::QuantityNode.new(node, 0, -1) }
          / node:primary opt_spaces_or_comments '+' { $$ = Packcr::Node::QuantityNode.new(node, 1, -1) }
          / node:primary opt_spaces_or_comments '?' { $$ = Packcr::Node::QuantityNode.new(node, 0, 1) }
          / node:primary { $$ = node }

primary <- var_name:identifier opt_spaces_or_comments ':' opt_spaces_or_comments name:identifier !( [ \t\v\f\r\n]* '<-' ) { $$ = Packcr::Node::ReferenceNode.new(name, var_name, $0sl.linenum, $0sl.charnum) }
         / name:identifier !( [ \t\v\f\r\n]* '<-' ) { $$ = Packcr::Node::ReferenceNode.new(name, nil, $0sl.linenum, $0sl.charnum) }
         / '(' opt_spaces_or_comments expr:expression opt_spaces_or_comments ')' { $$ = expr }
         / '<' opt_spaces_or_comments expr:expression opt_spaces_or_comments '>' { $$ = Packcr::Node::CaptureNode.new(expr) }
         / '$' <[1-9][0-9]*> { $$ = Packcr::Node::ExpandNode.new($1.to_i - 1, $0sl.linenum, $0sl.charnum) }
         / '.' { $$ = Packcr::Node::CharclassNode.new }
         / str:character_class { $$ = Packcr::Node::CharclassNode.new(Packcr.unescape_string(str, true)) }
         / str:quotation_single { $$ = Packcr::Node::StringNode.new(Packcr.unescape_string(str, false)) }
         / str:quotation_double { $$ = Packcr::Node::StringNode.new(Packcr.unescape_string(str, false)) }
         / code:lang_code_block { $$ = Packcr::Node::ActionNode.new(Packcr::CodeBlock.new(code, Packcr.find_trailing_blanks(code), $0sl.linenum, $0sl.charnum)) }

character_class <- '[' <( [^\\\[\]] / '\[' / '\]' / '\\' )*> ']' { $$ = $1 }

lang_code_block <- code:code_block { ____ = code }
                 / < identifier > '->' code:code_block { $$ = @lang == $1.to_sym ? code : nil  }
code_block <- '$' code:plain_code_block { $$ = code }
            / code:plain_code_block ${ ____ = code.gsub("$", @lang == :rb ? "__" : "_") }
plain_code_block <- '{' < codes? > '}' { $$ = $1 }
codes <- ([ \t\v\f\r\n]* code)+ [ \t\v\f\r\n]*
code <- codechar+
      / quotation_single
      / quotation_double
      / '{' code* '}'
codechar <- [^{}"'] / '\\\"' / '\\\'' / '\{' / '\}'
quotation_single <- '\'' < ( [^\\'\n] / '\\\'' / '\\\n' / '\\' [^\'\n] )*> '\'' { $$ = $1 }
quotation_double <- '\"' < ( [^\\"\n] / '\\\"' / '\\\n' / '\\' [^"\n] )*> '\"' { $$ = $1 }

identifier <- [a-zA-Z_] [a-zA-Z_0-9]* { $$ = $0 }

spaces <- ([ \t\v\f\r\n])+
opt_spaces_or_comments <- (comment / spaces)*
lf <- '\r'? '\n'

footer <- '%%' lf <.*> { @lsource << Packcr::CodeBlock.new($1, Packcr.find_trailing_blanks($1), $1sl.linenum, $1sl.charnum) }

%location {
  class Location
    attr_reader :charnum, :linenum, :linepos

    def initialize(charnum = 0, linenum = 0, linepos = 0)
      @charnum = charnum
      @linenum = linenum
      @linepos = linepos
    end

    def +(other)
      Location.new(other.charnum, other.linenum)
    end

    def -(other)
      Location.new(other.charnum, other.linenum)
    end

    def forward(buffer, cur, n)
      Location.new(@charnum, @linenum).forward!(buffer, cur, n)
    end

    def forward!(buffer, cur, n)
      buffer[cur, n].scan(/(.*)(\n)?/) do
        if Regexp.last_match[2]
          @linenum += 1
          @pos = 0
        else
          @charnum += Regexp.last_match[1].length
        end
      end
      self
    end
  end
}

%initialize {
  @location = []
  @lsource = []
  @init = []
  @utf8 = true
  @ascii = false
  @lang = :rb
  @rules = []
  @rulehash = {}
}
%%

class Packcr::Parser
  attr_accessor :rules

  def rule(name)
    @rulehash[name]
  end

  def make_rulehash
    @rules.each do |rule|
      @rulehash[rule.name] = rule
    end
  end
end
